`timescale 1ns/1ps


/*
作者 : ValentineHP
联系方式 : 微信公众号  FPGA之旅
*/




/*串口接收模块*/
module UART_MulRX #(
    parameter MulRXNum = 3
)(
    input                             sys_clk,        /*系统时钟 50M*/
    input                             rst_n,          /*系统复位*/

    output                            uart_rxs_done,   /*串口接收完成*/
    output[MulRXNum * 'd8 - 'd1:0]    odats,           /*接收数据*/

    input                             uartrx         /*uart rx数据线*/
);


wire        uart_rx_done;
wire[7:0]   odat;

reg[2:0]    MulRxCnt;
reg[MulRXNum * 'd8 - 'd1:0] RXDataReg;

/*数据接收完成*/
assign uart_rxs_done = ((MulRxCnt == (MulRXNum - 'd1)) && uart_rx_done == 1'b1) ? 1'b1 : 1'b0;

assign odats = RXDataReg;
/*接收到数据计数*/
always@(posedge sys_clk or negedge rst_n)
begin
    if(rst_n == 1'b0)
        MulRxCnt <= 'd0;
    else if(uart_rxs_done == 1'b1)
        MulRxCnt <= 'd0;
    else if(uart_rx_done == 1'b1) 
        MulRxCnt <= MulRxCnt + 1'b1;
    else
        MulRxCnt <= MulRxCnt;
end


/*接收数据寄存器*/
always@(posedge sys_clk or negedge rst_n)
begin
    if(rst_n == 1'b0)
        RXDataReg <= 'd0;
    else if(uart_rx_done == 1'b1)
        RXDataReg <= {odat,RXDataReg[MulRXNum * 'd8 - 'd1:8]};
    else
        RXDataReg <= RXDataReg;
end



/*串口接收模块*/
UART_RX #(
    .UARTBaud(115200)
)UART_RXHP(
    .sys_clk            (sys_clk),        /*系统时钟 50M*/
    .rst_n              (rst_n),          /*系统复位*/

    .uart_rx_done       (uart_rx_done),   /*串口接收完成*/
    .odat               (odat),           /*接收数据*/

    .uartrx             (uartrx)         /*uart rx数据线*/
);


endmodule